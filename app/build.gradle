apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.lordraydenmk.superheroesapp"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (project.hasProperty("marvel_public_api_key")) {
            def marvelPublicKey = '"' + project.property("marvel_public_api_key") + '"'
            def marvelPrivateKey = '"' + project.property("marvel_private_api_key") + '"'
            buildConfigField("String", "MARVEL_PUBLIC_API_KEY", marvelPublicKey)
            buildConfigField("String", "MARVEL_PRIVATE_API_KEY", marvelPrivateKey)
        } else {
            // the app needs `marvel_public_api_key` and `marvel_private_api_key`
            // as gradle properties to run
            // to get one visit: https://developer.marvel.com/account
            throw new GradleException("Please provide the Marvel API keys as gradle properties")
        }
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        test.java.srcDirs += "src/testFixtures/kotlin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.1.0"
    def lifecycle = "2.3.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle"
    implementation 'androidx.fragment:fragment-ktx:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    def nav_version = "2.3.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def arrow = "0.11.0"
    implementation "io.arrow-kt:arrow-core:$arrow"

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "io.coil-kt:coil:1.1.1"

    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation 'com.akaita.java:rxjava2-debug:1.4.0'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"
    implementation "com.github.akarnokd:rxjava2-extensions:0.20.10"

    def rxbinding = "3.1.0"
    implementation "com.jakewharton.rxbinding3:rxbinding:$rxbinding"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxbinding"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxbinding"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rxbinding"

    def retrofit = "2.9.0"
    def okhttp = "4.9.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"

    def kotest = "4.4.1"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}